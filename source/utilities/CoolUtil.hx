package utilities;

import shaders.ColorSwap;
import flixel.math.FlxMath;
import flixel.tweens.FlxEase;
import flixel.util.FlxStringUtil;
import sys.FileSystem;
import game.SongLoader.FNFCMetadata;
import game.FreeplaySong;
import sys.io.File;
import flixel.FlxG;
import flixel.text.FlxText;
import flixel.tweens.FlxTween;
import flixel.util.FlxColor;
import lime.app.Application;
import lime.graphics.Image;
import lime.utils.Assets;
import ui.logs.Logs;
import haxe.EntryPoint;
import haxe.Log;
import haxe.Json;
import haxe.PosInfos;

/**
 * Helper class with lots of utilitiy functions.
 */
@:cppFileCode('
	#include <iostream>
')
class CoolUtil {
	public static function boundTo(value:Float, min:Float, max:Float):Float {
		var newValue:Float = value;

		if (newValue < min)
			newValue = min;
		else if (newValue > max)
			newValue = max;

		return newValue;
	}

	public static function coolTextFileSys(path:String):Array<String> {
		#if sys
		var daList:Array<String> = File.getContent(path).trim().split('\n');

		for (i in 0...daList.length) {
			daList[i] = daList[i].trim();
		}

		return daList;
		#else
		return coolTextFile(path);
		#end
	}

	public static function coolTextFile(path:String):Array<String> {
		if (!Assets.exists(path)) {
			return [];
		}
		var daList:Array<String> = Assets.getText(path).trim().split('\n');

		for (i in 0...daList.length) {
			daList[i] = daList[i].trim();
		}

		return daList;
	}

	public static function coolTextFileOfArrays(path:String, ?delimeter:String = " "):Array<Array<String>> {
		if (!Assets.exists(path)) {
			return [[]];
		}
		var daListOg:Array<String> = coolTextFile(path);

		var daList:Array<Array<String>> = [];

		for (line in daListOg) {
			daList.push(line.split(delimeter));
		}

		return daList;
	}

	public static function numberArray(max:Int, ?min = 0):Array<Int> {
		var dumbArray:Array<Int> = [];

		for (i in min...max)
			dumbArray.push(i);

		return dumbArray;
	}

	public static function coolTextCase(text:String):String {
		var returnText:String = "";

		var textArray:Array<String> = text.split(" ");

		for (text in textArray) {
			var textStuffs = text.split("");

			for (i in 0...textStuffs.length) {
				if (i != 0)
					returnText += textStuffs[i].toLowerCase();
				else
					returnText += textStuffs[i].toUpperCase();
			}

			returnText += " ";
		}

		return returnText;
	}

	// stolen from psych lmao cuz i'm lazy
	public static function dominantColor(sprite:flixel.FlxSprite):Int {
		var countByColor:Map<Int, Int> = [];

		for (col in 0...sprite.frameWidth) {
			for (row in 0...sprite.frameHeight) {
				var colorOfThisPixel:Int = sprite.pixels.getPixel32(col, row);

				if (colorOfThisPixel != 0) {
					if (countByColor.exists(colorOfThisPixel))
						countByColor[colorOfThisPixel] = countByColor[colorOfThisPixel] + 1;
					else if (countByColor[colorOfThisPixel] != 13520687 - (2 * 13520687))
						countByColor[colorOfThisPixel] = 1;
				}
			}
		}

		var maxCount = 0;
		var maxKey:Int = 0; // after the loop this will store the max color

		countByColor[FlxColor.BLACK] = 0;

		for (key in countByColor.keys()) {
			if (countByColor[key] >= maxCount) {
				maxCount = countByColor[key];
				maxKey = key;
			}
		}

		return maxKey;
	}

	public static function dominantColorFrame(sprite:flixel.FlxSprite):Int {
		var countByColor:Map<Int, Int> = [];

		sprite.useFramePixels = true;
		for (col in 0...sprite.frameWidth) {
			for (row in 0...sprite.frameHeight) {
				var colorOfThisPixel:Int = sprite.framePixels.getPixel32(col, row);

				if (colorOfThisPixel != 0) {
					if (countByColor.exists(colorOfThisPixel))
						countByColor[colorOfThisPixel] = countByColor[colorOfThisPixel] + 1;
					else if (countByColor[colorOfThisPixel] != 13520687 - (2 * 13520687))
						countByColor[colorOfThisPixel] = 1;
				}
			}
		}

		var maxCount = 0;
		var maxKey:Int = 0; // after the loop this will store the max color

		countByColor[FlxColor.BLACK] = 0;

		for (key in countByColor.keys()) {
			if (countByColor[key] >= maxCount) {
				maxCount = countByColor[key];
				maxKey = key;
			}
		}

		return maxKey;
	}

	/**
	 * Sets the window icon
	 * @author Vortex
	 * @param path
	 */
	public static inline function setWindowIcon(path:String) {
		#if desktop
		FlxG.stage.window.setIcon(Image.fromFile(path));
		#end
	}

	/**
	 * Gets the highest number from a list.
	 * Shoutout to @crinfarr on Discord.
	 * @see https://discord.com/channels/162395145352904705/162395145352904705/1157847259858354236
	 * @param ...nums
	 * @return T
	 */
	public static function max<T:Float>(...nums:T):T {
		var max:Null<T> = null;
		for (n in nums) {
			if (max == null || n > max)
				max = n;
		}
		return max;
	}

	/**
	 * Gets the lowest number from a list.
	 * Shoutout to @crinfarr on Discord.
	 * @see https://discord.com/channels/162395145352904705/162395145352904705/1157847259858354236
	 * @param ...nums
	 * @return T
	 */
	public static function min<T:Float>(...nums:T):T {
		var min:Null<T> = null;
		for (n in nums) {
			if (min == null || n < min)
				min = n;
		}
		return min;
	}

	/**
	 * Converts rgb values into hsv values.
	 * @see https://math.stackexchange.com/questions/556341/rgb-to-hsv-color-conversion-algorithm
	 * @see https://github.com/python/cpython/blob/3.9/Lib/colorsys.py
	 * @param r The red value
	 * @param g The green value
	 * @param b The blue value
	 * @return Array<Int>
	 */
	public static function rgbToHsv(r:Int, g:Int, b:Int):Array<Int> {
		r = Std.int(r / 255);
		b = Std.int(b / 255);
		g = Std.int(g / 255);

		var maxc = max(r, g, b);
		var minc = min(r, g, b);

		var h;

		var v = maxc;

		if (minc == maxc)
			return [Std.int(0), Std.int(0), Std.int(v)];

		var s = (maxc - minc) / maxc;
		var rc = (maxc - r) / (maxc - minc);
		var gc = (maxc - g) / (maxc - minc);
		var bc = (maxc - b) / (maxc - minc);

		if (r == maxc) {
			h = 0.0 + bc - gc;
		} else if (g == maxc) {
			h = 2.0 + rc - bc;
		} else {
			h = 4.0 + gc - rc;
		}

		h = (h / 6.0) % 1.0;

		return [Std.int(h * 360), Std.int(s * 100), Std.int(v * 100)];
	}

	public static var errors(default, never):Map<String, FlxText> = new Map<String, FlxText>();

	/**
		Funny handler for `Application.current.window.alert` that *doesn't* crash on Linux and shit.

		@param message Message of the error.
		@param title Title of the error.

		@author Leather128
	**/
	public static function coolError(message:String, title:String, ?pos:PosInfos):Void {
		if (errors.exists(title + "\n\n" + message) || #if haxe5 errors.size() #else Lambda.count(errors) #end >= 10) {
			return;
		}

		print(title + "-" + message, ERROR, pos);

		var text:FlxText = new FlxText(0, 0, 1280, title + "\n\n" + message, 32);
		text.font = Paths.font("vcr.ttf");
		text.color = 0xFF0000;
		text.alignment = CENTER;
		text.borderSize = 1.5;
		text.borderStyle = OUTLINE_FAST;
		text.borderColor = FlxColor.BLACK;
		text.scrollFactor.set();
		text.cameras = [FlxG.cameras.list[FlxG.cameras.list.length - 1]];
		text.zIndex = FlxMath.MAX_VALUE_INT;
		errors.set(title + "\n\n" + message, text);

		FlxTween.tween(text, {alpha: 0, y: 64}, 4, {
			onComplete: function(_) {
				if (text != null && text.exists) {
					errors.remove(title + "\n\n" + message);
					FlxG.state.remove(text);
					text.destroy();
				}
			},
			startDelay: 1
		});

		FlxG.state.add(text);
	}

	/**
		Simple map that contains useful ansi color strings
		that can be used when printing to console for nice colors.

		@see https://en.wikipedia.org/wiki/ANSI_escape_code#Colors
	**/
	public static var ansi_colors:Map<String, String> = [
		'reset' => '\033[0m',
		'black' => '\033[30m',
		'red' => '\033[31m',
		'green' => '\033[32m',
		'yellow' => '\033[33m',
		'blue' => '\033[34m',
		'magenta' => '\033[35m',
		'cyan' => '\033[36m',
		'white' => '\033[37m',
		'bright_black' => '\033[90m',
		'bright_red' => '\033[91m',
		'bright_green' => '\033[92m',
		'bright_yellow' => '\033[93m',
		'bright_blue' => '\033[94m',
		'bright_magenta' => '\033[95m',
		'bright_cyan' => '\033[96m',
		'bright_white' => '\033[97m',
	];

	/**
		Used to replace haxe.Log.trace

		@param value Value to trace.
		@param pos_infos (Optional) Info about where the trace came from and parameters for it.

		@author Leather128
	**/
	public static function haxe_print(value:Dynamic, ?pos_infos:PosInfos):Void {
		if (pos_infos.customParams == null)
			print(value, LOG, pos_infos);
		else {
			var type:PrintType = pos_infos.customParams.copy()[0];
			pos_infos.customParams = null; // so no stupid shit in the end of prints :D
			print(Std.string(value), type, pos_infos);
		}
	}

	/**
		Prints the specified `message` with `type` and `pos_infos`.

		@param message The message to print as a `String`.
		@param type (Optional) The type of print (aka, `LOG`, `DEBUG`, `WARNING`, or `ERROR`) as a `PrintType`.
		@param pos_infos (Optional) Info about where the print came from.

		@author Leather128
	**/
	public static function print(message:String, ?type:PrintType = LOG, ?infos:PosInfos):Void {
		// 先输出到终端，使用 std::flush 确保立即输出（无需等待主线程）
		final consoleOutput:String = formatConsoleOutput(message, type, infos);
		untyped __cpp__("std::cout << {0} << std::flush", consoleOutput + '\n');

		// 游戏内日志必须在主线程更新（UI 操作）
		final formattedMessage:String = formatOutput(message, infos);
		EntryPoint.runInMainThread(() -> {
			switch (type) {
				case DEBUG:
					Logs.debug(formattedMessage);
				case WARNING:
					Logs.warn(formattedMessage);
				case ERROR:
					Logs.error(formattedMessage);
				default:
					Logs.log(formattedMessage);
			}
		});
	}

	/**
		Formats output for console with ANSI colors and proper structure.

		@param message The message to format.
		@param type The print type (LOG, DEBUG, WARNING, ERROR).
		@param pos_infos Position info for source location.
		@return Formatted console string.
	**/
	public static function formatConsoleOutput(message:String, type:PrintType, ?infos:PosInfos):String {
		final location:String = (infos != null && infos.fileName != null && infos.fileName.length > 0)
			? '${haxe.io.Path.normalize(infos.fileName)}:${infos.lineNumber}'
			: 'unknown';
		final cleanMessage:String = cleanAnsiEscapeCodes(message);

		// 获取短路径（仅显示文件名）
		final shortLocation:String = extractFileName(location);

		// 使用 ANSI 颜色代码美化输出
		final typeColor:String = getTypeColor(type);
		final locationColor:String = ansi_colors.get('bright_cyan') ?? ansi_colors.get('cyan');
		final messageColor:String = getMessageColor(type);
		final resetColor:String = ansi_colors.get('reset') ?? '';

		final typeLabel:String = switch(type) {
			case DEBUG: 'DEBUG';
			case WARNING: 'WARN ';
			case ERROR: 'ERROR';
			case LOG: 'LOG  ';
		}

		// 格式: [LOG] source/utilities/CoolUtil.hx:517: Garbage collection: 7.34MB freed
		return '${typeColor}[${typeLabel}]${resetColor} ${locationColor}${location}${resetColor}: ${messageColor}${cleanMessage}${resetColor}';
	}

	/**
		提取文件名从完整路径
	**/
	private static function extractFileName(fullPath:String):String {
		// 如果路径太长，只显示最后几个目录 + 文件名
		final parts:Array<String> = fullPath.replace('\\', '/').split('/');
		if (parts.length <= 2) {
			return fullPath;
		}
		// 保留最后两个层级，例如: source/utilities/File.hx
		return parts.slice(-2).join('/');
	}

	/**
		获取日志类型的颜色
	**/
	private static function getTypeColor(type:PrintType):String {
		return switch(type) {
			case DEBUG: ansi_colors.get('bright_green') ?? ansi_colors.get('green');
			case WARNING: ansi_colors.get('bright_yellow') ?? ansi_colors.get('yellow');
			case ERROR: ansi_colors.get('bright_red') ?? ansi_colors.get('red');
			case LOG: ansi_colors.get('bright_cyan') ?? ansi_colors.get('cyan');
		}
	}

	/**
		获取消息内容的颜色
	**/
	private static function getMessageColor(type:PrintType):String {
		return switch(type) {
			case DEBUG: ansi_colors.get('bright_white') ?? ansi_colors.get('white');
			case WARNING: ansi_colors.get('bright_yellow') ?? ansi_colors.get('yellow');
			case ERROR: ansi_colors.get('bright_red') ?? ansi_colors.get('red');
			case LOG: ansi_colors.get('white') ?? ansi_colors.get('white');
		}
	}

	/**
		清理消息中的 ANSI 转义码（防止双重转义）
	**/
	private static function cleanAnsiEscapeCodes(message:String):String {
		return ~/[\x1b\x9b]\[[0-9;]*[a-zA-Z]/g.replace(message, '');
	}

	public static function formatOutput(v:Dynamic, infos:PosInfos):String {
		var str = Std.string(v);
		if (infos == null)
			return str;
		var pstr = infos.fileName  + (infos.lineNumber >= 0 ? ':${infos.lineNumber}' : "");
		if (infos.customParams != null)
			for (v in infos.customParams)
				str += ", " + Std.string(v);
		return pstr + ": " + str;
	}

	/**
	 * Format the path to be more readable (shorten if needed)
	 */
	public static function formatPath(path:String):String {
		if (path == null || path.length <= 0)
			return "unknown";

		// Normalize path separators
		var normalized = path.replace('\\', '/');

		// Extract just the filename if the path is too long
		var parts = normalized.split('/');
		if (parts.length > 3) {
			return '.../${parts.slice(-2).join("/")}';
		}
		return normalized;
	}

	// messageFromPrintType 已被 formatConsoleOutput 取代，保留用于向后兼容
	public static function messageFromPrintType(?type:PrintType = LOG):String {
		var messagePrefix:String;
		switch (type) {
			case DEBUG:
				messagePrefix = '${ansi_colors["green"]} DEBUG';
			case WARNING:
				messagePrefix = '${ansi_colors["yellow"]} WARNING';
			case ERROR:
				messagePrefix = '${ansi_colors["red"]} ERROR';
			default:
				messagePrefix = '${ansi_colors["cyan"]} LOG';
		}
		return '$messagePrefix ${ansi_colors["default"] ?? ansi_colors["reset"]}';
	}

	/**
		Access to the old `haxe.Log.trace` function.

		@author Leather128
	**/
	public dynamic static function haxe_trace(v:Dynamic, ?infos:Null<PosInfos>) {}

	public static inline function getCurrentVersion():String {
		return 'v' + Application.current.meta.get('version');
	}

	public static function songExists(song:String, difficulty:String, ?mix:String):Bool {
		song = song.toLowerCase();
		difficulty = difficulty.toLowerCase();
		var exists:Bool = false;
		var songPath:String = 'song data/$song';
		var difficultyExtension:String = difficulty == 'normal' ? '' : '-$difficulty';
		var difficultyExtensionMeta:String = (difficulty == 'nightmare' || difficulty == 'erect') ? '-erect' : '';

		if (mix != null && Assets.exists('$difficultyExtensionMeta-$mix')) {
			difficultyExtensionMeta += '-$mix';
		}

		// legacy charts
		if (Assets.exists(Paths.json('$songPath/$song$difficultyExtension'))) {
			return true;
		} else {
			// no meta or no chart
			if (!Assets.exists(Paths.json('$songPath/$song-metadata$difficultyExtensionMeta'))
				|| !Assets.exists(Paths.json('$songPath/$song-chart$difficultyExtensionMeta'))) {
				return exists;
			}

			var meta:FNFCMetadata = cast Json.parse(Assets.getText(Paths.json('$songPath/$song-metadata$difficultyExtensionMeta')));
			if (meta.playData.difficulties.contains(difficulty)) {
				exists = true;
			}
		}

		return exists;
	}

	/**
	 * Clears all assets and other objects from the game's memory.
	 * Will skip clearing if "persistentCachedData" option is enabled.
	 * Garbage collection will ALWAYS run regardless of settings.
	 */
	public static function clearMemory():Void {
		// 支持新旧两种变量名以保持向后兼容性
		var keepCache:Bool = Options.getData('persistentCachedData') ?? Options.getData('memoryLeaks');
		
		var beforeMem:Float = 0;
		var clearedAssets:Bool = !keepCache;

		#if cpp
		beforeMem = external.memory.Memory.getCurrentUsage();
		#end

		if (!keepCache) {
			// Remove cached assets (prevents memory leaks that i can prevent)
			lime.utils.Assets.cache.clear();
			openfl.utils.Assets.cache.clear();
			#if MODDING_ALLOWED
			polymod.Polymod.clearCache();
			#end

			// Remove lingering sounds from the sound list
			FlxG.sound.list.forEachAlive(function(sound:flixel.sound.FlxSound):Void {
				FlxG.sound.list.remove(sound, true);
				sound.stop();
				sound.destroy();
			});
			FlxG.sound.list.clear();

			FlxG.bitmap.clearCache();

			// Clear actual assets from OpenFL and Lime itself
			var cache:openfl.utils.AssetCache = cast openfl.utils.Assets.cache;
			var lime_cache:lime.utils.AssetCache = cast lime.utils.Assets.cache;

			// this totally isn't copied from polymod/backends/OpenFLBackend.hx trust me
			for (key in cache.bitmapData.keys())
				cache.bitmapData.remove(key);
			for (key in cache.font.keys())
				cache.font.remove(key);

			for (key in cache.sound.keys()) {
				cache.sound.get(key).close();
				cache.sound.remove(key);
			}

			// this totally isn't copied from polymod/backends/LimeBackend.hx trust me
			for (key in lime_cache.image.keys())
				lime_cache.image.remove(key);
			for (key in lime_cache.font.keys())
				lime_cache.font.remove(key);
			for (key in lime_cache.audio.keys()) {
				lime_cache.audio.get(key).dispose();
				lime_cache.audio.remove(key);
			};

			ColorSwap.colorSwapCache.clear();

			Paths.graphics.clear();
		}

		// Run built-in garbage collector - ALWAYS runs regardless of persistentCachedData
		#if cpp
		cpp.vm.Gc.compact();
		#end
		#if sys
		openfl.system.System.gc();
		#end

		// Log memory cleanup statistics
		#if cpp
		var afterMem:Float = external.memory.Memory.getCurrentUsage();
		var freedMem:Float = beforeMem - afterMem;
		if (freedMem > 0) {
			if (clearedAssets) {
				trace('Memory cleared: ${FlxStringUtil.formatBytes(freedMem)} freed (assets + GC)');
			} else {
				trace('Garbage collection: ${FlxStringUtil.formatBytes(freedMem)} freed');
			}
		}
		#end
	}

	public static function convertFromFreeplaySongList() {
		final possibleLocations:Array<String> = [
			"data/freeplaySonglist.txt",
			"data/freeplaySongList.txt",
			"_append/data/freeplaySongList.txt",
			"_append/data/freeplaySonglist.txt"
		];

		inline function parseFreeplaySongList(list:Array<String>):Array<FreeplaySong> {
			var songs:Array<FreeplaySong> = [];
			for (i in 0...list.length) {
				if (list[i].trim() != "") {
					var listArray:Array<String> = list[i].split(":");

					var week:Int = Std.parseInt(listArray[2]);
					var icon:String = listArray[1];
					var song:String = listArray[0];

					var diffsStr:String = listArray[3];
					var diffs:Array<String> = ["easy", "normal", "hard"];

					var color:String = listArray[4] ?? "#00FF00";

					if (diffsStr != null)
						diffs = diffsStr.split(",");

					songs.push({
						name: song,
						week: week,
						icon: icon,
						difficulties: diffs,
						color: color,
						metadata: {
							composer: "",
							charter: "",
							modcharter: ""
						},
						menuConfig: {
							canBeEntered: true,
							showStats: true
						},
						extraData: {}
					});
				}
			}
			return songs;
		}

		var curMod:String = Options.getData("curMod");

		if (FileSystem.exists('./mods/$curMod/data/freeplay.json')) {
			return;
		}

		for (location in possibleLocations) {
			var pathToCheck:String = './mods/$curMod/$location';
			if (FileSystem.exists(pathToCheck)) {
				if (!FileSystem.exists('./mods/$curMod/data/')) {
					FileSystem.createDirectory('./mods/$curMod/data');
				}
				File.saveContent('./mods/$curMod/data/freeplay.json', Json.stringify({
					songs: parseFreeplaySongList(coolTextFileSys(pathToCheck)),
				}, "\t"));
				break;
			}
		}
	}

	public static function easeFromString(ease:String = 'linear'):EaseFunction {
		switch (ease.toLowerCase().trim()) {
			case 'backin':
				return FlxEase.backIn;
			case 'backinout':
				return FlxEase.backInOut;
			case 'backout':
				return FlxEase.backOut;
			case 'bouncein':
				return FlxEase.bounceIn;
			case 'bounceinout':
				return FlxEase.bounceInOut;
			case 'bounceout':
				return FlxEase.bounceOut;
			case 'circin':
				return FlxEase.circIn;
			case 'circinout':
				return FlxEase.circInOut;
			case 'circout':
				return FlxEase.circOut;
			case 'cubein':
				return FlxEase.cubeIn;
			case 'cubeinout':
				return FlxEase.cubeInOut;
			case 'cubeout':
				return FlxEase.cubeOut;
			case 'elasticin':
				return FlxEase.elasticIn;
			case 'elasticinout':
				return FlxEase.elasticInOut;
			case 'elasticout':
				return FlxEase.elasticOut;
			case 'expoin':
				return FlxEase.expoIn;
			case 'expoinout':
				return FlxEase.expoInOut;
			case 'expoout':
				return FlxEase.expoOut;
			case 'quadin':
				return FlxEase.quadIn;
			case 'quadinout':
				return FlxEase.quadInOut;
			case 'quadout':
				return FlxEase.quadOut;
			case 'quartin':
				return FlxEase.quartIn;
			case 'quartinout':
				return FlxEase.quartInOut;
			case 'quartout':
				return FlxEase.quartOut;
			case 'quintin':
				return FlxEase.quintIn;
			case 'quintinout':
				return FlxEase.quintInOut;
			case 'quintout':
				return FlxEase.quintOut;
			case 'sinein':
				return FlxEase.sineIn;
			case 'sineinout':
				return FlxEase.sineInOut;
			case 'sineout':
				return FlxEase.sineOut;
			case 'smoothstepin':
				return FlxEase.smoothStepIn;
			case 'smoothstepinout':
				return FlxEase.smoothStepInOut;
			case 'smoothstepout':
				return FlxEase.smoothStepOut;
			case 'smootherstepin':
				return FlxEase.smootherStepIn;
			case 'smootherstepinout':
				return FlxEase.smootherStepInOut;
			case 'smootherstepout':
				return FlxEase.smootherStepOut;
		}

		return FlxEase.linear;
	}
}

enum abstract PrintType(String) to String from String {
	var LOG = "LOG";
	var DEBUG = "DEBUG";
	var WARNING = "WARNING";
	var ERROR = "ERROR";
}
